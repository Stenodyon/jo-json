aspect Json {

	public static JsonUnknown JsonUnknown.INSTANCE = new JsonUnknown();

	/**
	 * Retrieves the value of a member in this JSON object.
	 * @param name the name to search for
	 * @return the JSON member value for the first member with the given name
	 */
	syn JsonValue JsonObject.get(String name) {
		for (JsonMember member: getMemberList()) {
			if (member.getName().equals(name)) {
				return member.getValue();
			}
		}
		return JsonUnknown.INSTANCE;
	}

	syn boolean JsonObject.hasMember(String name) {
		for (JsonMember member: getMemberList()) {
			if (member.getName().equals(name)) {
				return true;
			}
		}
		return false;
	}

	syn boolean JsonValue.isUnknown() = false;
	eq JsonUnknown.isUnknown() = true;

	syn JsonObject JsonValue.object() = new JsonObject();
	eq JsonObject.object() = this;

	syn JsonArray JsonValue.array() = new JsonArray();
	eq JsonArray.array() = this;

	public void JsonObject.addMember(String name, JsonValue value) {
		addMember(new JsonMember(name, value));
	}

	public void JsonObject.addMember(String name, String value) {
		addMember(new JsonMember(name, new JsonString(value)));
	}

	public void JsonObject.addMember(String name, int value) {
		addMember(new JsonMember(name, new JsonNumber(""+value)));
	}

	public void JsonArray.add(JsonValue element) {
		addElement(element);
	}

	/**
	 * @param undefined value returned if this is not a JSON string.
	 * @return the JSON string value
	 */
	syn String JsonValue.stringValue(String undefined) = undefined;
	eq JsonString.stringValue(String undefined) = getValue();

	/**
	 * @param undefined value returned if this is not a JSON number.
	 * @return the JSON number value
	 */
	syn int JsonValue.intValue(int undefined) = undefined;
	eq JsonNumber.intValue(int undefined) = Integer.parseInt(getValue());

	/**
	 * @param undefined value returned if this is not a JSON number.
	 * @return the JSON number value
	 */
	syn float JsonValue.floatValue(float undefined) = undefined;
	eq JsonNumber.floatValue(float undefined) = Float.parseFloat(getValue());

	/**
	 * @param undefined value returned if this is not a JSON number.
	 * @return the JSON number value
	 */
	syn double JsonValue.doubleValue(double undefined) = undefined;
	eq JsonNumber.doubleValue(double undefined) = Double.parseDouble(getValue());
}
